# secure-boot.nix
# Requires lanzaboote flake
{ lib, pkgs, ... }:
let
  apply-tpm = pkgs.writeShellApplication {
    name = "apply-tpm";
    runtimeInputs = with pkgs; [ cryptsetup systemd ];
    text = builtins.readFile ../scripts/apply-tpm.sh;
  };
in {

  boot.loader.efi.canTouchEfiVariables = true;
  boot.kernelPackages = pkgs.linuxPackages_latest;
  nixpkgs.config.allowUnfree = true;
  hardware.enableAllFirmware = true;
  boot.supportedFilesystems = [ "btrfs" "ntfs" ];

  # Quiet boot with plymouth - supports LUKS passphrase entry if needed
  boot.kernelParams = [
    "quiet"
    "rd.systemd.show_status=false"
    "rd.udev.log_level=3"
    "udev.log_priority=3"
    "boot.shell_on_fail"
  ];
  boot.consoleLogLevel = 0;
  boot.initrd.verbose = false;
  boot.plymouth.enable = true;

  # Bootspec and Secure Boot using lanzaboote
  #
  # See: https://github.com/nix-community/lanzaboote/blob/master/docs/QUICK_START.md
  #
  boot.bootspec.enable = true;
  boot.loader.systemd-boot.enable = lib.mkForce false;
  boot.lanzaboote = {
    enable = true;
    pkiBundle = "/etc/secureboot";
  };

  # TPM for unlocking LUKS
  #
  # TPM kernel module must be enabled for initrd. Device driver is viewable via the command:
  # sudo systemd-cryptenroll --tpm2-device=list
  # And added to a device's configuration:
  # boot.initrd.kernelModules = [ "tpm_tis" ];
  #
  # Must be enabled by hand - e.g.
  # sudo systemd-cryptenroll --wipe-slot=tpm2 /dev/nvme0n1p3 --tpm2-device=auto --tpm2-pcrs=0+2+7
  #
  security.tpm2.enable = true;
  security.tpm2.tctiEnvironment.enable = true;

  environment.systemPackages = lib.mkBefore [ apply-tpm ];

  # Add personal yubikey IDs for wiring up PAM (not enabled here)
  security.pam.yubico.mode = lib.mkDefault "challenge-response";
  security.pam.yubico.id = lib.mkDefault [
    "17965018"
    "19870941"
    "19870894"
  ];
}

